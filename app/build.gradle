plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    buildFeatures {
        buildConfig = true
    }

    signingConfigs {
        AceKeystore {
            keyAlias System.getenv("KEY_ALIAS") ?: keystoreProperties['keyAlias']
            keyPassword System.getenv("KEY_PASSWORD") ?: keystoreProperties['keyPassword']
            storeFile System.getenv("STORE_FILE") ? file("../keystore.jks") : file(keystoreProperties['storeFile'])
            storePassword System.getenv("STORE_PASSWORD") ?: keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "io.github.acedroidx.frp"
        minSdkVersion 23
        targetSdkVersion 35
        compileSdk 35
        versionCode 4
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        signingConfig signingConfigs.AceKeystore

        buildConfigField("String", "FrpVersion", '"0.61.1"')
        buildConfigField("String", "FrpcFileName", '"libfrpc.so"')
        buildConfigField("String", "LogFileName", '"frpc.log"')
        buildConfigField("String", "ConfigFileName", '"config.toml"')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.AceKeystore
        }
        debug {
            signingConfig signingConfigs.AceKeystore
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a", "x86_64", "armeabi-v7a"
            universalApk true
        }
    }
    namespace 'io.github.acedroidx.frp'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}